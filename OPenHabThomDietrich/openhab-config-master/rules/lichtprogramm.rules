
val String filename = "lichtprogramm.rules"

rule "Lichtprogramm Init"
when
    System started
then
    createTimer(now.plusSeconds(270)) [|
      Lights.members.filter[item | item.state == NULL].forEach[item | item.sendCommand(OFF)]
    ]
end

rule "Schranktaster Arbeitsbeleuchtung ON"
when
    Item KU_Taster_Schrank_2Short changed to ON
then
    if (KU_Arbeit.state == OFF) {
        KU_Arbeit.sendCommand(ON)
    } else if (KU_Edison_Dimmer_Level.state != 70) {
        KU_Edison_Dimmer_Level.sendCommand(70)
    } else {
        Kueche_Deckenlicht.sendCommand(ON)
    }
end


rule "Schranktaster Arbeitsbeleuchtung OFF"
when
    Item KU_Taster_Schrank_1Short changed to ON
then
    KU_Arbeit.sendCommand(OFF)
    Kueche_Deckenlicht.sendCommand(OFF)
    if (KU_Edison_Dimmer_Level.state == 30) KU_Edison_Dimmer_Level.sendCommand(OFF)
    if (KU_Edison_Dimmer_Level.state == 70) KU_Edison_Dimmer_Level.sendCommand(30)
end


rule "Schranktaster Hauptleuchte ON"  when Item KU_Taster_Schrank_2Long changed to ON then Kueche_Deckenlicht.sendCommand(ON) end
rule "Schranktaster Hauptleuchte OFF" when Item KU_Taster_Schrank_1Long changed to ON then Kueche_Deckenlicht.sendCommand(OFF) end


rule "Whg_Light_Preset (Sitemap -> Homematic)"
when
  Item Whg_Light_Preset received command
then
    //logInfo(filename, "Whg_Light_Preset (Sitemap -> Homematic).")
    switch (receivedCommand){
        case "OFF": {
            Lights.members.filter[item | item.state != OFF].forEach[item | item.sendCommand(OFF)]
            logInfo(filename, "Lichtprogramm 0 - Aus")
        }
        case "NORMAL": {
            BK_Balkonmagic.sendCommand(ON)
            WZ_Lacks_LEDs.sendCommand(ON)
            WZ_Origami.sendCommand(ON)
            WZ_Expedit.sendCommand(ON)
            SZ_Orakel.sendCommand(ON)
            KU_Arbeit.sendCommand(ON)
            KU_Edison_Dimmer_Level.sendCommand(70)
            WZ_Goldfruchtpalme.sendCommand(OFF)
            WZ_Lacks_Lampe.sendCommand(OFF)
            Flur_Deckenlicht.sendCommand(OFF)
            Kueche_Deckenlicht.sendCommand(OFF)
            logInfo(filename, "Lichtprogramm 1 - Normal")
        }
        case "RELAX": {
            BK_Balkonmagic.sendCommand(ON)
            WZ_Lacks_LEDs.sendCommand(ON)
            WZ_Origami.sendCommand(ON)
            WZ_Expedit.sendCommand(ON)
            KU_Arbeit.sendCommand(ON)
            KU_Edison_Dimmer_Level.sendCommand(30)
            WZ_Goldfruchtpalme.sendCommand(OFF)
            WZ_Lacks_Lampe.sendCommand(OFF)
            Flur_Deckenlicht.sendCommand(OFF)
            Kueche_Deckenlicht.sendCommand(OFF)
            SZ_Deckenlicht.sendCommand(OFF)
            logInfo(filename, "Lichtprogramm 2 - Relax")
        }
        case "FULL": {
            Lights.members.filter[item | item.state != ON].forEach[item | item.sendCommand(ON)]
            logInfo(filename, "Lichtprogramm 3 - Voll")
        }
    }
end


// Homematic 8-Taster Fernbedienung Wohnzimmer
rule "Fernbedienung Regel 1"
when
    Item Fernbedienung_Taste1 received update ON
then
    logInfo(filename, "Fernbedienung 1.Reihe links")
    WZ_Origami.sendCommand(ON)
    WZ_Lacks_LEDs.sendCommand(ON)
    WZ_Expedit.sendCommand(ON)
end
rule "Fernbedienung Regel 2"
when
    Item Fernbedienung_Taste2 received update ON
then
    logInfo(filename, "Fernbedienung 1.Reihe rechts")
    WZ_Origami.sendCommand(OFF)
    WZ_Lacks_LEDs.sendCommand(OFF)
    WZ_Expedit.sendCommand(OFF)
end
rule "Fernbedienung Regel 3"
when
    Item Fernbedienung_Taste3 received update ON
then
    logInfo(filename, "Fernbedienung 2.Reihe links")
    BK_Balkonmagic.sendCommand(ON)
end
rule "Fernbedienung Regel 4"
when
    Item Fernbedienung_Taste4 received update ON
then
    logInfo(filename, "Fernbedienung 2.Reihe rechts")
    BK_Balkonmagic.sendCommand(OFF)
end
rule "Fernbedienung Regel 5"
when
    Item Fernbedienung_Taste5 received update ON
then
    logInfo(filename, "Fernbedienung 3.Reihe links")
    KU_Arbeit.sendCommand(ON)
    KU_Edison_Dimmer_Level.sendCommand(70)
end
rule "Fernbedienung Regel 6"
when
    Item Fernbedienung_Taste6 received update ON
then
    logInfo(filename, "Fernbedienung 3.Reihe rechts")
    KU_Arbeit.sendCommand(OFF)
    KU_Edison_Dimmer_Level.sendCommand(0)
end
rule "Fernbedienung Regel 7"
when
    Item Fernbedienung_Taste7 received update ON
then
    logInfo(filename, "Fernbedienung 4.Reihe links")
    Whg_Light_Preset.sendCommand("RELAX")
end
rule "Fernbedienung Regel 8"
when
    Item Fernbedienung_Taste8 received update ON
then
    logInfo(filename, "Fernbedienung 4.Reihe rechts")
    Whg_Light_Preset.sendCommand("OFF")
end


// Homematic Schlüsselanhänger
rule "Schlüsselanhänger Taste 1"
when
    Item Keychain_Taste1 changed to ON
then
    logInfo(filename, "Schlüsselanhänger Taste 1 - Haus")
end
rule "Schlüsselanhänger Taste 2"
when
    Item Keychain_Taste2 changed to ON
then
    logInfo(filename, "Schlüsselanhänger Taste 2 - Haus mit Person")
end
rule "Schlüsselanhänger Taste 3"
when
    Item Keychain_Taste3 changed to ON
then
    logInfo(filename, "Schlüsselanhänger Taste 3 - Ein/Aus")
    Whg_Light_Preset.sendCommand("OFF")
end
rule "Schlüsselanhänger Taste 4"
when
    Item Keychain_Taste4 changed to ON
then
    logInfo(filename, "Schlüsselanhänger Taste 4 - Licht")
    Whg_Light_Preset.sendCommand("RELAX")
end

// Wohnzimmer Taster Eingang
rule "Wohnzimmer Taster oben kurz"
when
    Item WZ_Taster_2Short changed to ON
then
    WZ_Origami.sendCommand(ON)
    WZ_Lacks_LEDs.sendCommand(ON)
    WZ_Expedit.sendCommand(ON)
    BK_Balkonmagic.sendCommand(ON)
    WZ_Lacks_Lampe.sendCommand(OFF)
end

rule "Wohnzimmer Taster unten kurz"
when
    Item WZ_Taster_1Short changed to ON
then
    WZ_Origami.sendCommand(OFF)
    WZ_Lacks_LEDs.sendCommand(OFF)
    WZ_Expedit.sendCommand(OFF)
    BK_Balkonmagic.sendCommand(OFF)
    WZ_Lacks_Lampe.sendCommand(OFF)
end


// Flurtaster links (Fuse) und rechts (Tokyo)
rule "Flur beide Taster oben kurz"
when
    Item KU_Taster_Tokyo_2Short changed to ON or
    Item KU_Taster_Fuse_2Short changed to ON
then
    Whg_Light_Preset.sendCommand("RELAX")
    KU_Sonos_Control.sendCommand(PLAY)
end

rule "Flur beide Taster oben lang"
when
    Item KU_Taster_Tokyo_2Long changed to ON or
    Item KU_Taster_Fuse_2Long changed to ON
then
    Whg_Light_Preset.sendCommand("NORMAL")
    KU_Sonos_Control.sendCommand(PLAY)
end

rule "Flur beide Taster unten kurz"
when
    Item KU_Taster_Tokyo_1Short changed to ON or
    Item KU_Taster_Fuse_1Short changed to ON
then
    Whg_Light_Preset.sendCommand("OFF")
    KU_Sonos_Control.sendCommand(PAUSE)
end

rule "Flur beide Taster unten lang"
when
    Item KU_Taster_Tokyo_1Long changed to ON or
    Item KU_Taster_Fuse_1Long changed to ON
then
    Whg_Light_Preset.sendCommand("OFF")
    KU_Sonos_Control.sendCommand(PAUSE)
end

// vim: syntax=Xtend
