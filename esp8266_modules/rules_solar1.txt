On rules#timer=2 do
  gpio,2,1
  if [switch#pmode]=0
     gpio,2,1 
     if [a0#a0] > 3.9 // if enough power send more frequently
       let,14,15
     else
       let,14,60
     endif
     publish %sysname%/status,"%sysname%/%ip% [a0#a0]V %rssi%dBm now sleep for %v14% s"
     deepsleep,%v14%
  else       
     gpio,2,0
     publish %sysname%/status,"%sysname%/%ip% [a0#a0]V %rssi%dBm (in program mode)"
     timerset,1,1 // just send info
  endif

endon  

on wifi#disconnect do
  pulse,2,0,1600
  if [switch#pmode]=0
    timerset,2,1 // sleep
  endif
endon



On switch#pmode do
  // if switch programming mode=OFF goto BED
  if [switch#pmode]=0
    timerset,2,1
  else 
    gpio,2,0
    timerset,1,20
  endif
endon

on system#wake do
  timerset,1,1
  timerset,2,1
endon

On startSleep do
  timerset,1,1
  publish %sysname%/status,'%sysname%/%ip% EVENT startSleepMode RECEIVED'
  gpio,2,1
  delay,1000
  //pulse,2,0,200
  timerset,2,1
endon

On rules#timer=1 do
    delay,5000
    if [MQTT#connected]=1
      pulse,2,0,100
      Publish %sysname%/time/value,%systime%
//    publish %sysname%/bmp085/press,[bmp085#press]
      publish %sysname%/bmp085/temp,[bmp085#temp]
      publish %sysname%/a0/a0,[a0#a0]
    else 
      pulse,2,0,800
    endif
  if [switch#pmode]=1 // programming mode
    gpio,2,0
    timerset,1,20 // repeat sending mqtt
  else 
    gpio,2,1
  endif

endon
