
On rules#timer=2 do
  serialsend,'TIMER 2'
  gpio,2,1
  if [switch#pmode]=0
     gpio,2,1 
     if [a0#a0] > 3.9 // if enough power send more frequently
       let,14,600
     else
       let,14,4300
     endif
     delay,5000 // wait for establishing wlan
     publish %sysname%/status,"%sysname%/%ip% [a0#a0]V %rssi%dBm now sleep for %v14% s"
     if [var#6]=1 // sent?
       deepsleep,%v14%
     else 
       //try again after some time
       timerset,2,30
     endif
  else       
     gpio,2,0
     publish %sysname%/status,"%sysname%/%ip% [a0#a0]V %rssi%dBm (in program mode)"
     timerset,1,1
  endif
endon  

on MQTT#Connected do 
      publish %sysname%/time/value,%systime%
//    publish %sysname%/bmp085/press,[bmp085#press]
      publish %sysname%/bme/temp,[bme#temp]
      publish %sysname%/a0/a0,[a0#a0]
      let,6,1 // set the 'sent'flag
endon

on wifi#disconnect do
  pulse,2,0,1600
  if [switch#pmode]=0
    timerset,2,1 // sleep
  endif
endon


On switch#pmode do
  // if switch programming mode=OFF goto BED
  if [switch#pmode]=1
    serialsend,'pmode TRUE'
    gpio,2,0
    timerset,1,1
  else 
    serialsend,'pmode FALSE'
    timerset,2,1
  endif
endon

on system#wake do
  let,6,0
  timerset,2,1
endon

on system#boot do
  let,6,0
  timerset,2,1
endon

On rules#timer=1 do
  publish %sysname%/time/value,%systime%
  publish %sysname%/bme/temp,[bme#temp]
  publish %sysname%/a0/a0,[a0#a0]
  if [switch#pmode]=1 // programming mode
    gpio,2,0
    publish %sysname%/status,"%sysname%/%ip% [a0#a0]V %rssi%dBm (in program mode)"
    timerset,1,20 // repeat sending mqtt
  else 
    timerset,2,1
  endif
endon
