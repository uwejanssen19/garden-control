On rules#timer=2 do
  gpio,2,1
  if [switch#pmode]=0
     gpio,2,1 
     if [a0#a0] > 3.9 // if enough power send more frequently
       let,14,6
     else
       let,14,120
     endif
     publish %sysname%/status,"%sysname%/%ip% [a0#a0]V %rssi%dBm now sleep for %v14% s"
     deepsleep,%v14%
  else       
     gpio,2,0
     publish %sysname%/status,"%sysname%/%ip% [a0#a0]V %rssi%dBm (in program mode)"
     timerset,1,1 // just send info
  endif

endon  

On switch#pmode do
  // if switch programming mode=OFF goto BED
  if [switch#pmode]=0
    timerset,2,1
  endif
endon

on system#wake do
  timerset,1,1
  timerset,2,10
endon

On startSleep do
  timerset,1,1
  publish %sysname%/status,'%sysname%/%ip% EVENT startSleepMode RECEIVED'
  gpio,2,1
  delay,1000
  //pulse,2,0,200
  timerset,2,1
endon

On rules#timer=1 do
    delay,5000
    Publish %sysname%/time/value,%systime%
    publish %sysname%/bme/press,[bme#press]
    publish %sysname%/bme/temp,[bme#temp]
    publish %sysname%/a0/a0,[a0#a0]
endon



